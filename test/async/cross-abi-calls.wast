;; This test pairs sync and async-callback calls from $Bottom into $Top,
;; testing different numbers of parameters and results that hit the various
;; flat-vs-heap cases in the spec.
(component definition $C
  (component $Top
    (core module $Memory
      (memory (export "mem") 1)
      (func (export "realloc") (param i32 i32 i32 i32) (result i32)
        i32.const 0 ;; cheat -- there's never more than 1 allocation alive
      )
    )
    (core instance $memory (instantiate $Memory))
    (core module $Core
      (import "" "mem" (memory 0))
      (import "" "task.return0" (func $task.return0))
      (import "" "task.return1" (func $task.return1 (param f64)))
      (import "" "task.return16" (func $task.return16 (param i32 i64 f32 f64 i32 i64 f32 f64 i32 i64 f32 f64 i32 i64 f32 f64)))
      (import "" "task.return17" (func $task.return17 (param i32)))
      (func (export "sync-4-param") (param i32 i64 f32 f64)
        (if (i32.ne (i32.const 42) (local.get 0))
          (then unreachable))
        (if (i64.ne (i64.const 43) (local.get 1))
          (then unreachable))
        (if (f32.ne (f32.const 44.4) (local.get 2))
          (then unreachable))
        (if (f64.ne (f64.const 45.5) (local.get 3))
          (then unreachable))
      )
      (func (export "sync-5-param") (param i32 i64 f32 f64 i32)
        (if (i32.ne (i32.const -1) (local.get 0))
          (then unreachable))
        (if (i64.ne (i64.const -2) (local.get 1))
          (then unreachable))
        (if (f32.ne (f32.const -3.3) (local.get 2))
          (then unreachable))
        (if (f64.ne (f64.const -4.4) (local.get 3))
          (then unreachable))
        (if (i32.ne (i32.const -5) (local.get 4))
          (then unreachable))
      )
      (func $check-17 (param $ptr i32)
        (if (i32.ne (i32.const -1) (i32.load (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -2) (i64.load offset=8 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -3.3) (f32.load offset=16 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -4.4) (f64.load offset=24 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -5) (i32.load offset=32 (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -6) (i64.load offset=40 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -7.7) (f32.load offset=48 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -8.8) (f64.load offset=56 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -9) (i32.load offset=64 (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -10) (i64.load offset=72 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -11.11) (f32.load offset=80 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -12.12) (f64.load offset=88 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -13) (i32.load offset=96 (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -14) (i64.load offset=104 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -15.15) (f32.load offset=112 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -16.16) (f64.load offset=120 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -17) (i32.load offset=128 (local.get $ptr)))
          (then unreachable))
      )
      (func (export "sync-17-param") (param $ptr i32)
        (call $check-17 (local.get $ptr))
      )
      (func (export "sync-1-result") (result f64)
        (f64.const -1.1)
      )
      (func $setup-16 (param $ptr i32)
        (i32.store (local.get $ptr) (i32.const -1))
        (i64.store offset=8 (local.get $ptr) (i64.const -2))
        (f32.store offset=16 (local.get $ptr) (f32.const -3.3))
        (f64.store offset=24 (local.get $ptr) (f64.const -4.4))
        (i32.store offset=32 (local.get $ptr) (i32.const -5))
        (i64.store offset=40 (local.get $ptr) (i64.const -6))
        (f32.store offset=48 (local.get $ptr) (f32.const -7.7))
        (f64.store offset=56 (local.get $ptr) (f64.const -8.8))
        (i32.store offset=64 (local.get $ptr) (i32.const -9))
        (i64.store offset=72 (local.get $ptr) (i64.const -10))
        (f32.store offset=80 (local.get $ptr) (f32.const -11.11))
        (f64.store offset=88 (local.get $ptr) (f64.const -12.12))
        (i32.store offset=96 (local.get $ptr) (i32.const -13))
        (i64.store offset=104 (local.get $ptr) (i64.const -14))
        (f32.store offset=112 (local.get $ptr) (f32.const -15.15))
        (f64.store offset=120 (local.get $ptr) (f64.const -16.16))
      )
      (func $setup-17 (param $ptr i32)
        (call $setup-16 (local.get $ptr))
        (i32.store offset=128 (local.get $ptr) (i32.const -17))
      )
      (func (export "sync-16-result") (result i32)
        (local $ptr i32)
        (call $setup-16 (local.get $ptr))
        (local.get $ptr)
      )
      (func (export "sync-17-result") (result i32)
        (local $ptr i32)
        (call $setup-17 (local.get $ptr))
        (local.get $ptr)
      )
      (func (export "async-4-param") (param i32 i64 f32 f64) (result i32)
        (if (i32.ne (i32.const 42) (local.get 0))
          (then unreachable))
        (if (i64.ne (i64.const 43) (local.get 1))
          (then unreachable))
        (if (f32.ne (f32.const 44.4) (local.get 2))
          (then unreachable))
        (if (f64.ne (f64.const 45.5) (local.get 3))
          (then unreachable))
        (call $task.return0)
        (i32.const 0 (; EXIT ;))
      )
      (func (export "async-5-param") (param i32 i64 f32 f64 i32) (result i32)
        (if (i32.ne (i32.const -1) (local.get 0))
          (then unreachable))
        (if (i64.ne (i64.const -2) (local.get 1))
          (then unreachable))
        (if (f32.ne (f32.const -3.3) (local.get 2))
          (then unreachable))
        (if (f64.ne (f64.const -4.4) (local.get 3))
          (then unreachable))
        (if (i32.ne (i32.const -5) (local.get 4))
          (then unreachable))
        (call $task.return0)
        (i32.const 0 (; EXIT ;))
      )
      (func (export "async-17-param") (param $ptr i32) (result i32)
        (call $check-17 (local.get $ptr))
        (call $task.return0)
        (i32.const 0 (; EXIT ;))
      )
      (func (export "async-1-result") (result i32)
        (call $task.return1 (f64.const -1.1))
        (i32.const 0 (; EXIT ;))
      )
      (func (export "async-16-result") (result i32)
        (call $task.return16 (i32.const -1) (i64.const -2) (f32.const -3.3) (f64.const -4.4)
                             (i32.const -5) (i64.const -6) (f32.const -7.7) (f64.const -8.8)
                             (i32.const -9) (i64.const -10) (f32.const -11.11) (f64.const -12.12)
                             (i32.const -13) (i64.const -14) (f32.const -15.15) (f64.const -16.16))
        (i32.const 0 (; EXIT ;))
      )
      (func (export "async-17-result") (result i32)
        (local $ptr i32)
        (call $setup-17 (local.get $ptr))
        (call $task.return17 (local.get $ptr))
        (i32.const 0 (; EXIT ;))
      )
      (func (export "unreachable-cb") (param i32 i32 i32) (result i32)
        unreachable
      )
    )
    (canon task.return (core func $task.return0))
    (canon task.return (result f64) (core func $task.return1))
    (canon task.return (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64)) (core func $task.return16))
    (canon task.return (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32)) (memory $memory "mem") (core func $task.return17))
    (core instance $core (instantiate $Core (with "" (instance
      (export "mem" (memory $memory "mem"))
      (export "task.return0" (func $task.return0))
      (export "task.return1" (func $task.return1))
      (export "task.return16" (func $task.return16))
      (export "task.return17" (func $task.return17))
    ))))
    (func (export "sync-4-param") (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64)
      (canon lift (core func $core "sync-4-param"))
    )
    (func (export "sync-5-param") (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64) (param "e" u32)
      (canon lift (core func $core "sync-5-param"))
    )
    (func (export "sync-17-param") (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64)
                                   (param "e" u32) (param "f" u64) (param "g" f32) (param "h" f64)
                                   (param "i" u32) (param "j" u64) (param "k" f32) (param "l" f64)
                                   (param "m" u32) (param "n" u64) (param "o" f32) (param "p" f64)
                                   (param "q" u32)
      (canon lift (core func $core "sync-17-param") (memory $memory "mem") (realloc (func $memory "realloc")))
    )
    (func (export "sync-1-result") (result f64)
      (canon lift (core func $core "sync-1-result"))
    )
    (func (export "sync-16-result") (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64))
      (canon lift (core func $core "sync-16-result") (memory $memory "mem"))
    )
    (func (export "sync-17-result") (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32))
      (canon lift (core func $core "sync-17-result") (memory $memory "mem"))
    )
    (func (export "async-4-param") (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64)
      (canon lift (core func $core "async-4-param") async (callback (func $core "unreachable-cb")))
    )
    (func (export "async-5-param") (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64) (param "e" u32)
      (canon lift (core func $core "async-5-param") async (callback (func $core "unreachable-cb")))
    )
    (func (export "async-17-param") (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64)
                                    (param "e" u32) (param "f" u64) (param "g" f32) (param "h" f64)
                                    (param "i" u32) (param "j" u64) (param "k" f32) (param "l" f64)
                                    (param "m" u32) (param "n" u64) (param "o" f32) (param "p" f64)
                                    (param "q" u32)
      (canon lift (core func $core "async-17-param") async (callback (func $core "unreachable-cb")) (memory $memory "mem") (realloc (func $memory "realloc")))
    )
    (func (export "async-1-result") (result f64)
      (canon lift (core func $core "async-1-result") async (callback (func $core "unreachable-cb")))
    )
    (func (export "async-16-result") (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64))
      (canon lift (core func $core "async-16-result") async (callback (func $core "unreachable-cb")))
    )
    (func (export "async-17-result") (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32))
      (canon lift (core func $core "async-17-result") async (callback (func $core "unreachable-cb")) (memory $memory "mem") (realloc (func $memory "realloc")))
    )
  )
  (component $Bottom
    (import "func-4-param" (func $func-4-param (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64)))
    (import "func-5-param" (func $func-5-param (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64) (param "e" u32)))
    (import "func-17-param" (func $func-17-param (param "a" u32) (param "b" u64) (param "c" f32) (param "d" f64)
                                                 (param "e" u32) (param "f" u64) (param "g" f32) (param "h" f64)
                                                 (param "i" u32) (param "j" u64) (param "k" f32) (param "l" f64)
                                                 (param "m" u32) (param "n" u64) (param "o" f32) (param "p" f64)
                                                 (param "q" u32)))
    (import "func-1-result" (func $func-1-result (result f64)))
    (import "func-16-result" (func $func-16-result (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64))))
    (import "func-17-result" (func $func-17-result (result (tuple u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32 u64 f32 f64 u32))))
    (core module $Memory (memory (export "mem") 1))
    (core instance $memory (instantiate $Memory))
    (core module $Core
      (import "" "mem" (memory 1))
      (import "" "sync-4-param" (func $sync-4-param (param i32 i64 f32 f64)))
      (import "" "async-4-param" (func $async-4-param (param i32 i64 f32 f64) (result i32)))
      (import "" "sync-5-param" (func $sync-5-param (param i32 i64 f32 f64 i32)))
      (import "" "async-5-param" (func $async-5-param (param i32) (result i32)))
      (import "" "sync-17-param" (func $sync-17-param (param i32)))
      (import "" "async-17-param" (func $async-17-param (param i32) (result i32)))
      (import "" "sync-1-result" (func $sync-1-result (result f64)))
      (import "" "async-1-result" (func $async-1-result (param i32) (result i32)))
      (import "" "sync-16-result" (func $sync-16-result (param i32)))
      (import "" "async-16-result" (func $async-16-result (param i32) (result i32)))
      (import "" "sync-17-result" (func $sync-17-result (param i32)))
      (import "" "async-17-result" (func $async-17-result (param i32) (result i32)))
      (func (export "call-sync-4-param") (result i32)
        (call $sync-4-param (i32.const 42) (i64.const 43) (f32.const 44.4) (f64.const 45.5))
        (i32.const 83)
      )
      (func (export "call-async-4-param") (result i32)
        (if (i32.ne (call $async-4-param (i32.const 42) (i64.const 43) (f32.const 44.4) (f64.const 45.5))
                    (i32.const 2 (; RETURNED ;)))
          (then unreachable))
        (i32.const 84)
      )
      (func (export "call-sync-5-param") (result i32)
        (call $sync-5-param (i32.const -1) (i64.const -2) (f32.const -3.3) (f64.const -4.4) (i32.const -5))
        (i32.const 85)
      )
      (func (export "call-async-5-param") (result i32)
        (local $ptr i32)
        (i32.store (local.get $ptr) (i32.const -1))
        (i64.store offset=8 (local.get $ptr) (i64.const -2))
        (f32.store offset=16 (local.get $ptr) (f32.const -3.3))
        (f64.store offset=24 (local.get $ptr) (f64.const -4.4))
        (i32.store offset=32 (local.get $ptr) (i32.const -5))
        (if (i32.ne (call $async-5-param (local.get $ptr)) (i32.const 2 (; RETURNED ;)))
          (then unreachable))
        (i32.const 86)
      )
      (func $setup-17 (param $ptr i32)
        (i32.store (local.get $ptr) (i32.const -1))
        (i64.store offset=8 (local.get $ptr) (i64.const -2))
        (f32.store offset=16 (local.get $ptr) (f32.const -3.3))
        (f64.store offset=24 (local.get $ptr) (f64.const -4.4))
        (i32.store offset=32 (local.get $ptr) (i32.const -5))
        (i64.store offset=40 (local.get $ptr) (i64.const -6))
        (f32.store offset=48 (local.get $ptr) (f32.const -7.7))
        (f64.store offset=56 (local.get $ptr) (f64.const -8.8))
        (i32.store offset=64 (local.get $ptr) (i32.const -9))
        (i64.store offset=72(local.get $ptr) (i64.const -10))
        (f32.store offset=80 (local.get $ptr) (f32.const -11.11))
        (f64.store offset=88 (local.get $ptr) (f64.const -12.12))
        (i32.store offset=96 (local.get $ptr) (i32.const -13))
        (i64.store offset=104 (local.get $ptr) (i64.const -14))
        (f32.store offset=112 (local.get $ptr) (f32.const -15.15))
        (f64.store offset=120 (local.get $ptr) (f64.const -16.16))
        (i32.store offset=128 (local.get $ptr) (i32.const -17))
      )
      (func (export "call-sync-17-param") (result i32)
        (call $setup-17 (i32.const 0))
        (call $sync-17-param (i32.const 0))
        (i32.const 87)
      )
      (func (export "call-async-17-param") (result i32)
        (call $setup-17 (i32.const 0))
        (if (i32.ne (call $async-17-param (i32.const 0)) (i32.const 2 (; RETURNED ;)))
          (then unreachable))
        (i32.const 88)
      )
      (func (export "call-sync-1-result") (result i32)
        (if (f64.ne (call $sync-1-result) (f64.const -1.1))
          (then unreachable))
        (i32.const 89)
      )
      (func (export "call-async-1-result") (result i32)
        (local $ptr i32)
        (if (i32.ne (call $async-1-result (local.get $ptr)) (i32.const 2 (; RETURNED ;)))
          (then unreachable))
        (if (f64.ne (f64.load (local.get $ptr)) (f64.const -1.1))
          (then unreachable))
        (i32.const 90)
      )
      (func $check-16 (param $ptr i32)
        (if (i32.ne (i32.const -1) (i32.load (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -2) (i64.load offset=8 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -3.3) (f32.load offset=16 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -4.4) (f64.load offset=24 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -5) (i32.load offset=32 (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -6) (i64.load offset=40 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -7.7) (f32.load offset=48 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -8.8) (f64.load offset=56 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -9) (i32.load offset=64 (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -10) (i64.load offset=72 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -11.11) (f32.load offset=80 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -12.12) (f64.load offset=88 (local.get $ptr)))
          (then unreachable))
        (if (i32.ne (i32.const -13) (i32.load offset=96 (local.get $ptr)))
          (then unreachable))
        (if (i64.ne (i64.const -14) (i64.load offset=104 (local.get $ptr)))
          (then unreachable))
        (if (f32.ne (f32.const -15.15) (f32.load offset=112 (local.get $ptr)))
          (then unreachable))
        (if (f64.ne (f64.const -16.16) (f64.load offset=120 (local.get $ptr)))
          (then unreachable))
      )
      (func $check-17 (param $ptr i32)
        (call $check-16 (local.get $ptr))
        (if (i32.ne (i32.const -17) (i32.load offset=128 (local.get $ptr)))
          (then unreachable))
      )
      (func (export "call-sync-16-result") (result i32)
        (local $ptr i32)
        (call $sync-16-result (local.get $ptr))
        (call $check-16 (local.get $ptr))
        (i32.const 91)
      )
      (func (export "call-async-16-result") (result i32)
        (local $ptr i32)
        (if (i32.ne (call $async-16-result (local.get $ptr)) (i32.const 2 (; RETURNED ;)))
          (then unreachable))
        (call $check-16 (local.get $ptr))
        (i32.const 92)
      )
      (func (export "call-sync-17-result") (result i32)
        (local $ptr i32)
        (call $sync-17-result (local.get $ptr))
        (call $check-17 (local.get $ptr))
        (i32.const 93)
      )
      (func (export "call-async-17-result") (result i32)
        (local $ptr i32)
        (if (i32.ne (call $async-17-result (local.get $ptr)) (i32.const 2 (; RETURNED ;)))
          (then unreachable))
        (call $check-17 (local.get $ptr))
        (i32.const 94)
      )
    )
    (canon lower (func $func-4-param) (core func $sync-4-param))
    (canon lower (func $func-4-param) async (memory $memory "mem") (core func $async-4-param))
    (canon lower (func $func-5-param) (core func $sync-5-param))
    (canon lower (func $func-5-param) async (memory $memory "mem") (core func $async-5-param))
    (canon lower (func $func-17-param) (memory $memory "mem") (core func $sync-17-param))
    (canon lower (func $func-17-param) async (memory $memory "mem") (core func $async-17-param))
    (canon lower (func $func-1-result) (core func $sync-1-result))
    (canon lower (func $func-1-result) async (memory $memory "mem") (core func $async-1-result))
    (canon lower (func $func-16-result) (memory $memory "mem") (core func $sync-16-result))
    (canon lower (func $func-16-result) async (memory $memory "mem") (core func $async-16-result))
    (canon lower (func $func-17-result) (memory $memory "mem") (core func $sync-17-result))
    (canon lower (func $func-17-result) async (memory $memory "mem") (core func $async-17-result))
    (core instance $core (instantiate $Core (with "" (instance
      (export "mem" (memory $memory "mem"))
      (export "sync-4-param" (func $sync-4-param))
      (export "async-4-param" (func $async-4-param))
      (export "sync-5-param" (func $sync-5-param))
      (export "async-5-param" (func $async-5-param))
      (export "sync-17-param" (func $sync-17-param))
      (export "async-17-param" (func $async-17-param))
      (export "sync-1-result" (func $sync-1-result))
      (export "async-1-result" (func $async-1-result))
      (export "sync-16-result" (func $sync-16-result))
      (export "async-16-result" (func $async-16-result))
      (export "sync-17-result" (func $sync-17-result))
      (export "async-17-result" (func $async-17-result))
    ))))
    (func (export "call-sync-4-param") (result u32) (canon lift (core func $core "call-sync-4-param")))
    (func (export "call-async-4-param") (result u32) (canon lift (core func $core "call-async-4-param")))
    (func (export "call-sync-5-param") (result u32) (canon lift (core func $core "call-sync-5-param")))
    (func (export "call-async-5-param") (result u32) (canon lift (core func $core "call-async-5-param")))
    (func (export "call-sync-17-param") (result u32) (canon lift (core func $core "call-sync-17-param")))
    (func (export "call-async-17-param") (result u32) (canon lift (core func $core "call-async-17-param")))
    (func (export "call-sync-1-result") (result u32) (canon lift (core func $core "call-sync-1-result")))
    (func (export "call-async-1-result") (result u32) (canon lift (core func $core "call-async-1-result")))
    (func (export "call-sync-16-result") (result u32) (canon lift (core func $core "call-sync-16-result")))
    (func (export "call-async-16-result") (result u32) (canon lift (core func $core "call-async-16-result")))
    (func (export "call-sync-17-result") (result u32) (canon lift (core func $core "call-sync-17-result")))
    (func (export "call-async-17-result") (result u32) (canon lift (core func $core "call-async-17-result")))
  )
  (instance $top (instantiate $Top))
  (instance $bottom-to-sync (instantiate $Bottom
    (with "func-4-param" (func $top "sync-4-param"))
    (with "func-5-param" (func $top "sync-5-param"))
    (with "func-17-param" (func $top "sync-17-param"))
    (with "func-1-result" (func $top "sync-1-result"))
    (with "func-16-result" (func $top "sync-16-result"))
    (with "func-17-result" (func $top "sync-17-result"))
  ))
  (instance $bottom-to-async (instantiate $Bottom
    (with "func-4-param" (func $top "async-4-param"))
    (with "func-5-param" (func $top "async-5-param"))
    (with "func-17-param" (func $top "async-17-param"))
    (with "func-1-result" (func $top "async-1-result"))
    (with "func-16-result" (func $top "async-16-result"))
    (with "func-17-result" (func $top "async-17-result"))
  ))
  (func (export "sync-calls-sync-4-param") (alias export $bottom-to-sync "call-sync-4-param"))
  (func (export "sync-calls-async-4-param") (alias export $bottom-to-async "call-sync-4-param"))
  (func (export "async-calls-sync-4-param") (alias export $bottom-to-sync "call-async-4-param"))
  (func (export "async-calls-async-4-param") (alias export $bottom-to-async "call-async-4-param"))
  (func (export "sync-calls-sync-5-param") (alias export $bottom-to-sync "call-sync-5-param"))
  (func (export "sync-calls-async-5-param") (alias export $bottom-to-async "call-sync-5-param"))
  (func (export "async-calls-sync-5-param") (alias export $bottom-to-sync "call-async-5-param"))
  (func (export "async-calls-async-5-param") (alias export $bottom-to-async "call-async-5-param"))
  (func (export "sync-calls-sync-17-param") (alias export $bottom-to-sync "call-sync-17-param"))
  (func (export "sync-calls-async-17-param") (alias export $bottom-to-async "call-sync-17-param"))
  (func (export "async-calls-sync-17-param") (alias export $bottom-to-sync "call-async-17-param"))
  (func (export "async-calls-async-17-param") (alias export $bottom-to-async "call-async-17-param"))
  (func (export "sync-calls-sync-1-result") (alias export $bottom-to-sync "call-sync-1-result"))
  (func (export "sync-calls-async-1-result") (alias export $bottom-to-async "call-sync-1-result"))
  (func (export "async-calls-sync-1-result") (alias export $bottom-to-sync "call-async-1-result"))
  (func (export "async-calls-async-1-result") (alias export $bottom-to-async "call-async-1-result"))
  (func (export "sync-calls-sync-16-result") (alias export $bottom-to-sync "call-sync-16-result"))
  (func (export "sync-calls-async-16-result") (alias export $bottom-to-async "call-sync-16-result"))
  (func (export "async-calls-sync-16-result") (alias export $bottom-to-sync "call-async-16-result"))
  (func (export "async-calls-async-16-result") (alias export $bottom-to-async "call-async-16-result"))
  (func (export "sync-calls-sync-17-result") (alias export $bottom-to-sync "call-sync-17-result"))
  (func (export "sync-calls-async-17-result") (alias export $bottom-to-async "call-sync-17-result"))
  (func (export "async-calls-sync-17-result") (alias export $bottom-to-sync "call-async-17-result"))
  (func (export "async-calls-async-17-result") (alias export $bottom-to-async "call-async-17-result"))
)

(component instance $i $C)
(assert_return (invoke "sync-calls-sync-4-param") (u32.const 83))
(component instance $i $C)
(assert_return (invoke "sync-calls-async-4-param") (u32.const 83))
(component instance $i $C)
(assert_return (invoke "async-calls-sync-4-param") (u32.const 84))
(component instance $i $C)
(assert_return (invoke "async-calls-async-4-param") (u32.const 84))
(component instance $i $C)
(assert_return (invoke "sync-calls-sync-5-param") (u32.const 85))
(component instance $i $C)
(assert_return (invoke "sync-calls-async-5-param") (u32.const 85))
(component instance $i $C)
(assert_return (invoke "async-calls-sync-5-param") (u32.const 86))
(component instance $i $C)
(assert_return (invoke "async-calls-async-5-param") (u32.const 86))
(component instance $i $C)
(assert_return (invoke "sync-calls-sync-17-param") (u32.const 87))
(component instance $i $C)
(assert_return (invoke "sync-calls-async-17-param") (u32.const 87))
(component instance $i $C)
(assert_return (invoke "async-calls-sync-17-param") (u32.const 88))
(component instance $i $C)
(assert_return (invoke "async-calls-async-17-param") (u32.const 88))
(component instance $i $C)
(assert_return (invoke "sync-calls-sync-1-result") (u32.const 89))
(component instance $i $C)
(assert_return (invoke "sync-calls-async-1-result") (u32.const 89))
(component instance $i $C)
(assert_return (invoke "async-calls-sync-1-result") (u32.const 90))
(component instance $i $C)
(assert_return (invoke "async-calls-async-1-result") (u32.const 90))
(component instance $i $C)
(assert_return (invoke "sync-calls-sync-16-result") (u32.const 91))
(component instance $i $C)
(assert_return (invoke "sync-calls-async-16-result") (u32.const 91))
(component instance $i $C)
(assert_return (invoke "async-calls-sync-16-result") (u32.const 92))
(component instance $i $C)
(assert_return (invoke "async-calls-async-16-result") (u32.const 92))
(component instance $i $C)
(assert_return (invoke "sync-calls-sync-17-result") (u32.const 93))
(component instance $i $C)
(assert_return (invoke "sync-calls-async-17-result") (u32.const 93))
(component instance $i $C)
(assert_return (invoke "async-calls-sync-17-result") (u32.const 94))
(component instance $i $C)
(assert_return (invoke "async-calls-async-17-result") (u32.const 94))
